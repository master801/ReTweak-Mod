buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
        maven {
            name = 'sonatype'
            url = 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
    }
    dependencies {
        classpath(
                'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
        )
        classpath(
                'net.sf.proguard:proguard-gradle:5.2.1'
        )
        classpath(
                group: 'io.franzbecker',
                name: 'gradle-lombok',
                version: '1.8'
        )
    }
}

apply plugin: 'java'
apply plugin: 'forge'
apply plugin: 'io.franzbecker.gradle-lombok'

version = project.properties['version']
group = 'org.slave.minecraft'
archivesBaseName = 'ReTweak-Mod'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

minecraft {
    version = project.properties['forge-version']
    runDir = 'run'

    replace '@MOD_VERSION@', project.version
}

jar {
    manifest {
        attributes 'Created-By': 'Master801'
        attributes 'FMLCorePlugin': 'org.slave.minecraft.retweak.asm.ReTweakASM'
        attributes 'FMLCorePluginContainsFMLMod': 'true'
    }
}

dependencies {
    compile(
            group: 'org.slf4j',
            name: 'slf4j-api',
            version: project.properties['slf4j-version']
    )
    compile(
            group: 'org.apache.logging.log4j',
            name: 'log4j-slf4j-impl',
            version: project.properties['log4j-version']
    )

    compile fileTree(
            dir: 'libs',
            exclude: [
                    '*-src.jar'
            ],
            include: [
                    '*.jar'
            ]
    )
}

processResources {
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task proguard(type: proguard.gradle.ProGuardTask) {
    configuration 'configuration.pro'
}

task mkSrcJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'src'
}

task mkDevJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
}

build.dependsOn mkSrcJar, mkDevJar
