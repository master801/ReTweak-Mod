buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'java'
apply plugin: 'forge'

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

version = project.properties['version']
group = 'org.slave.minecraft'
archivesBaseName = 'ReTweak-Mod'

minecraft {
    version = project.properties['forge-version']
    runDir = 'run'

    replace '@MOD_VERSION@', project.version
}

jar {
    manifest {
        attributes 'Created-By' : 'Master801'
        attributes 'FMLCorePlugin' : 'org.slave.minecraft.retweak.asm.ReTweakASM'
        attributes 'FMLCorePluginContainsFMLMod' : 'true'
    }
}

dependencies {
    compile group: 'org.slf4j', name: 'slf4j-api', version: project.properties['sl4j-version']
    compile group: 'com.github.pwittchen.kirai', name: 'library', version: project.properties['kirari-version']
}

processResources {
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task makeSourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'src'
}

task makeDeobfuscatedJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
}

build.dependsOn makeSourcesJar, makeDeobfuscatedJar
